!>
................................................................................
.    Copyright (c) 2009-2024 Crater Dog Technologies™.  All Rights Reserved.   .
................................................................................
.  DO NOT ALTER OR REMOVE COPYRIGHT NOTICES OR THIS FILE HEADER.               .
.                                                                              .
.  This code is free software; you can redistribute it and/or modify it under  .
.  the terms of The MIT License (MIT), as published by the Open Source         .
.  Initiative. (See http://opensource.org/licenses/MIT)                        .
................................................................................
<!

!>
    GO PACKAGE NOTATION
    This document is a formal definition of Go Package Notation™ (GoPN) using
    Crater Dog Syntax Notation™ (CDSN).

    TOKEN DEFINITIONS
    The following token definitions are used by the scanner to generate the
    stream of tokens that are processed by the parser.  Each token name begins
    with an upper case letter.  Unlike with rule definitions, a token definition
    cannot specify the name of a rule within its definition but it can specify
    the name of other tokens.

    The following intrinsic token types are environment or language specific:
     * ANY - Any language specific character.
     * LOWER - Any language specific lower case character.
     * UPPER - Any language specific upper case character.
     * DIGIT - Any language specific digit.
     * ESCAPE - Any environment specific escape sequence.
     * CONTROL - Any environment specific (non-printable) control character.
     * EOL - The environment specific end-of-line character.
     * EOF - The environment specific end-of-file marker.

    Token definitions cannot be recursive and the scanning of tokens is not
    greedy.  Any spaces within a token definition are NOT ignored.
<!

$COMMENT: "/*" EOL ANY* EOL "*/" EOL+  ! Chooses the shortest possible match.

$DELIMITER: "[" | "]" | "(" | ")" | "{" | "}" | "." | "," | "="

$IDENTIFIER: (LOWER | UPPER) (LOWER | UPPER | DIGIT)*

$NOTE: "//" (~CONTROL)*

$TEXT: '"' ANY* '"'  ! Chooses the shortest possible match.

!>
    RULE DEFINITIONS
    The following rules are used by the parser when parsing the stream of tokens
    generated by the scanner.  Each rule name begins with a lower case letter.
    The rule definitions may specify the names of tokens or other rules and are
    matched by the parser in the order listed.  A rule definition may also be
    directly or indirectly recursive.  The sequence of factors within in a rule
    definition may be separated by spaces which are ignored by the parser.

    A predicate within a factor may also be constrained by any of the following
    cardinalities:
     * predicate{M} - Exactly M instances of the specified predicate.
     * predicate{M..} - M or more instances of the specified predicate.
     * predicate{M..N} - M to N instances of the specified predicate.
     * predicate? - Zero or one instances of the specified predicate.
     * predicate* - Zero or more instances of the specified predicate.
     * predicate+ - One or more instances of the specified predicate.

    Inversion within a rule definition may only be applied to a token predicate
    resulting in a single token.  Rule inversions cannot be nested.
<!

$source: package EOF  ! Terminated with an end-of-file marker.

$package: copyright header imports? types? interfaces?

$copyright: COMMENT

$header: COMMENT "package" IDENTIFIER

$imports: "import" "(" modules? ")"

$modules: module+

$module: IDENTIFIER TEXT

$types: "// TYPES" specializations? functionals?

$specializations: "// Specializations" specialization+

$specialization: declaration abstraction enumeration?

$declaration: COMMENT "type" IDENTIFIER ("[" parameters "]")?

$parameters: parameter ("," parameter)* ","?

$parameter: IDENTIFIER abstraction

$abstraction: prefix? IDENTIFIER ("[" arguments "]")?

$prefix: "[" "]" | "map" "[" IDENTIFIER "]" | "chan" | IDENTIFIER "."

$arguments: abstraction ("," abstraction)* ","?

$enumeration: "const" "(" values ")"

$values: parameter "=" "iota" IDENTIFIER*

$functionals: "// Functionals" functional+

$functional: declaration "func" "(" parameters? ")" result

$result: abstraction | "(" parameters ")"

$interfaces: "// INTERFACES" aspects? classes? instances?

$aspects: "// Aspects" aspect+

$aspect: declaration "interface" "{" methods? "}"

$classes: "// Classes" class+

$class: declaration "interface" "{" constants? constructors? functions? "}"

$constants: "// Constants" constant+

$constant: IDENTIFIER "(" ")" abstraction

$constructors: "// Constructors" constructor+

$constructor: IDENTIFIER "(" parameters? ")" abstraction

$functions: "// Functions" function+

$function: IDENTIFIER "(" parameters? ")" result

$instances: "// Instances" instance+

$instance: declaration "interface" "{" attributes? abstractions? methods? "}"

$attributes: "// Attributes" attribute+

$attribute: IDENTIFIER "(" parameter? ")" abstraction?

$abstractions: "// Abstractions" abstraction+

$methods: "// Methods" method+

$method: IDENTIFIER "(" parameters? ")" result?

